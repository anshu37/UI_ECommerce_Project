pipeline {
    agent any

    stages {
        stage('Checkout') {
            steps {
                checkout scm
            }
        }

        stage('Run Tests') {
            steps {
                bat 'mvn clean test'
            }
        }

        stage('Generate Email Summary') {
            steps {
                script {
                    def json = readJSON file: 'target/cucumber-report/cucumber.json'

                    int total = 0
                    int passed = 0
                    int failed = 0
                    int index = 1

                    String tableRows = ""

                    json.each { feature ->
                        feature.elements.each { scenario ->
                            total++
                            boolean scenarioFailed = false

                            scenario.steps.each { step ->
                                if (step.result.status == 'failed') {
                                    scenarioFailed = true
                                }
                            }

                            if (scenarioFailed) {
                                failed++
                                tableRows += """
                                <tr style="background-color:#f8d7da;">
                                    <td>${index}</td>
                                    <td>${scenario.name}</td>
                                    <td><b>FAILED</b></td>
                                </tr>
                                """
                            } else {
                                passed++
                                tableRows += """
                                <tr style="background-color:#d4edda;">
                                    <td>${index}</td>
                                    <td>${scenario.name}</td>
                                    <td><b>PASSED</b></td>
                                </tr>
                                """
                            }
                            index++
                        }
                    }

                    env.EMAIL_BODY = """
                    <h2>UI Automation Execution Summary</h2>

                    <table border="1" cellpadding="8" cellspacing="0">
                        <tr><td><b>Total Scenarios</b></td><td>${total}</td></tr>
                        <tr style="background-color:#d4edda;"><td><b>Passed</b></td><td>${passed}</td></tr>
                        <tr style="background-color:#f8d7da;"><td><b>Failed</b></td><td>${failed}</td></tr>
                    </table>

                    <br/>

                    <h3>Scenario Details</h3>
                    <table border="1" cellpadding="8" cellspacing="0">
                        <tr>
                            <th>S.No</th>
                            <th>Scenario</th>
                            <th>Status</th>
                        </tr>
                        ${tableRows}
                    </table>

                    <br/>
                    <a href="${env.BUILD_URL}cucumber-html-reports/overview-features.html">
                        View Full Cucumber Report
                    </a>
                    """
                }
            }
        }
    }

    post {
        always {
            emailext(
                subject: "UI Automation | ${env.JOB_NAME} | Build #${env.BUILD_NUMBER} | ${currentBuild.currentResult}",
                body: "${env.EMAIL_BODY}",
                mimeType: 'text/html',
                to: 'anshu.1997mishra@gmail.com'
            )
        }
    }
}
